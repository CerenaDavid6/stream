// model and data interfaces

{
	println: println
	default: default
	slice: slice
	map: map
	filter: filter
	every: every
} := import('std')
{
	lower: lower
	split: split
	contains?: strContains?
	join: join
} := import('str')
fs := import('fs')
json := import('json')
datetime := import('datetime')

config := import('config')
StreamFile := config.StreamFile

if fs.statFile(StreamFile) {
	? -> if fs.writeFile(StreamFile, '') {
		true -> println('Created database at ' + StreamFile)
		_ -> {
			println('Could not create database!')
			exit(1)
		}
	}
}

fn intoUpdates(s) {
	lines := []
	buf := ''
	parse := json.parse
	fn sub(i) if c := s.(i) {
		? -> lines << parse(buf)
		'\n' -> sub(i + 1, lines << parse(buf), buf <- '')
		_ -> sub(i + 1, buf << c)
	}
	sub(0)
	lines |> filter(fn(u) u != :error)
}

fn appendUpdate(status, withRes) {
	updateLine := json.serialize({
		t: int(time())
		s: status
	}) << '\n'
	fs.appendFile(StreamFile, updateLine, withRes)
}

fn updatesAtTime(t, withUpdates) with exec('grep', [
	'-si'
	string(t)
	StreamFile
], '') fn(evt) if evt.type {
	:error -> withUpdates(?)
	_ -> evt.stdout |> intoUpdates() |> filter(fn(u) u.t = t) |> withUpdates()
}

fn deleteAtTime(t, withRes) with fs.readFile(StreamFile) fn(file) if file {
	? -> withRes(?)
	_ -> {
		updates := file |> intoUpdates()
		deletedUpdates := updates |> with filter() fn(u) u.t != t

		updatedLines := deletedUpdates |>
			map(json.serialize) |>
			join('\n') + '\n' // appended log lines always trail with a '\n'
		fs.writeFile(StreamFile, updatedLines, withRes)
	}
}

fn latestUpdates(page, count, searchQuery, withUpdates) {
	with fs.readFile(StreamFile) fn(file) if file {
		? -> withUpdates(?)
		_ -> {
			updates := file |> intoUpdates()
			if {
				count < 0 -> updates
				_ -> {
					keywords := searchQuery |>
						default('') |>
						split(' ') |>
						filter(fn(s) s != '') |>
						map(lower)
					updates <- if keywords {
						[] -> updates
						_ -> updates |> with filter() fn(update) {
							s := update.s |> lower()
							keywords |> with every() fn(word) s |> strContains?(word)
						}
					}

					// pagination and slicing
					start := len(updates) - page * count
					end := start + count
					withUpdates(
						updates |> slice(start, end)
						// previous page exists?
						end < len(updates)
						// next page exists?
						start > 0
					)
				}
			}
		}
	}
}

fn updatesOnDay(year, month, day, withUpdates) {
	minTime := datetime.timestamp({
		year: year, month: month, day: day
		hour: 0, minute: 0, second: 0
	})
	maxTime := minTime + 86400 - 1

	with fs.readFile(StreamFile) fn(file) if file {
		? -> withUpdates(?)
		_ -> file |>
			intoUpdates() |>
			filter(fn(update) update.t >= minTime & update.t <= maxTime) |>
			withUpdates()
	}
}

